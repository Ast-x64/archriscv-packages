# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Michal Bozon <michal.bozon__at__gmail.com>

pkgname=libyaml
pkgver=0.2.1
pkgrel=1
pkgdesc="YAML 1.1 library"
arch=('x86_64')
url="https://pyyaml.org/wiki/LibYAML"
license=('MIT')
source=("https://pyyaml.org/download/libyaml/yaml-$pkgver.tar.gz")
md5sums=('72724b9736923c517e5a8fc6757ef03d')

build() {
  cd "$srcdir/yaml-$pkgver"
  cp /usr/share/libtool/build-aux/config.guess config/
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/yaml-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

