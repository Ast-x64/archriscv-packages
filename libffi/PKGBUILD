# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.3rc0
_pkgver=3.3-rc0
pkgrel=0
pkgdesc='Portable foreign function interface library'
arch=('x86_64')
url='https://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
source=(https://github.com/libffi/libffi/releases/download/v$_pkgver/libffi-$_pkgver.tar.gz)
sha1sums=('2eb8dbfed2cdf74dfa511b3facfee8ad5b1a35a3')

build() {
  cd $pkgname-$_pkgver
  ./configure --prefix=/usr --disable-static \
    --enable-pax_emutramp
  make
}

check() {
  make -C $pkgname-$_pkgver check
}

package() {
  cd $pkgname-$_pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
