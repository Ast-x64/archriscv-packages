# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Thomas Bächler <thomas@archlinux.org>

pkgname=mkinitcpio-busybox
pkgver=1.31.1
pkgrel=1
pkgdesc='Base initramfs tools'
arch=(x86_64)
url="https://www.busybox.net/"
license=(GPL)
depends=(glibc)
options=(!buildflags)
source=(https://busybox.net/downloads/busybox-$pkgver.tar.bz2{,.sig}
        config)
sha256sums=('d0f940a72f648943c1f2211e0e3117387c31d765137d92bd8284a3fb9752a998'
            'SKIP'
            'd97a1c31e658431de8f1bfdbd13ba6115211ac35d20d174239fb188015d3656f')
validpgpkeys=('C9E9416F76E610DBD09D040F47B70C55ACC9965B') # Denis Vlasenko <vda.linux@googlemail.com>

prepare() {
  cd "busybox-$pkgver"

  cp "$srcdir/config" .config
}

build() {
  # reproducible build
  export KCONFIG_NOTIMESTAMP=1
  make -C "busybox-$pkgver"
}

package() {
  install -Dm755 "$srcdir/busybox-$pkgver/busybox" "$pkgdir/usr/lib/initcpio/busybox"
}
