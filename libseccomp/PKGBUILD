# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgname=libseccomp
pkgver=2.4.3.20200614
_commit=6b286c2e8e43de76746346b8eab855311915f5aa
pkgrel=1
pkgdesc='Enhanced seccomp library'
arch=('x86_64')
license=('LGPL2.1')
url="https://github.com/seccomp/libseccomp"
depends=('glibc')
checkdepends=('valgrind')
makedepends=('git')
provides=('libseccomp.so')
#source=(https://github.com/seccomp/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
source=("git+https://github.com/seccomp/${pkgname}?commit=_commit")
sha256sums=('SKIP')
# XXX: do NOT trust this key as its not yet in the strong set, see linked issue
# XXX: https://github.com/seccomp/libseccomp/issues/187
# XXX: 47A68FCE37C7D7024FD65E11356CE62C2B524099  Tom Hromatka <tom.hromatka@oracle.com>
# XXX: do NOT trust ^^ this key as its not yet in the strong set ^^ see linked issue
validpgpkeys=('7100AADFAE6E6E940D2E0AD655E45A5AE8CA7C8A') # Paul Moore <paul@paul-moore.com>

build() {
  cd ${pkgname}
  autoreconf -fiv
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}
  make check
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 CHANGELOG README.md SECURITY.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
